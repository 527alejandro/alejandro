load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin")
load(":local_packages_util.bzl", "link_local_packages")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@bazel_skylib//lib:collections.bzl", "collections")

exports_files([
    "firebase.json",
    "ngsw-config.json",
    "ngsw-config.template.json",
    "tsconfig.json",
])

# If set will use first party angular deps for aio targets instead of their npm equivalent
bool_flag(
    name = "flag_aio_local_deps",
    build_setting_default = False,
)

config_setting(
    name = "aio_local_deps",
    flag_values = {
        ":flag_aio_local_deps": "true",
    },
)

# All source and configuration files required to build the docs app
APPLICATION_FILES = [
    "angular.json",
    "tsconfig.app.json",
    "tsconfig.json",
] + glob(
    ["src/**/*"],
    exclude = [
        "src/**/*.spec.ts",
        # Temporarily exclude generated sources produced by the non-bazel
        # build until the whole project is built by bazel and this directory
        # isn't needed.
        "src/generated/**/*",
    ],
)

# Dependencies required to build the docs app.
APPLICATION_DEPS = [
    ":stackblitz",
    "@npm//@angular-devkit/build-angular",
    "@npm//@angular-eslint/builder",
    "@npm//@angular/animations",
    "@npm//@angular/cdk",
    "@npm//@angular/cli",
    "@npm//@angular/common",
    "@npm//@angular/compiler",
    "@npm//@angular/compiler-cli",
    "@npm//@angular/core",
    "@npm//@angular/elements",
    "@npm//@angular/forms",
    "@npm//@angular/material",
    "@npm//@angular/platform-browser",
    "@npm//@angular/platform-browser-dynamic",
    "@npm//@angular/router",
    "@npm//@angular/service-worker",
    "@npm//@types/lunr",
    "@npm//@types/trusted-types",
    "@npm//lunr",
    "@npm//rxjs",
    "@npm//safevalues",
    "@npm//tslib",
    "@npm//zone.js",
]

# All sources, specs, and config files required to test the docs app
TEST_FILES = APPLICATION_FILES + [
    "karma.conf.js",
    "tsconfig.spec.json",
] + glob(
    ["src/**/*.spec.ts"],
)

# Dependencies required to test the docs app
TEST_DEPS = APPLICATION_DEPS + [
    "@npm//@angular/build-tooling/bazel/browsers/chromium",
    "@npm//@types/jasmine",
    "@npm//@types/node",
    "@npm//assert",
    "@npm//jasmine",
    "@npm//jasmine-core",
    "@npm//karma-chrome-launcher",
    "@npm//karma-coverage",
    "@npm//karma-jasmine",
    "@npm//karma-jasmine-html-reporter",
    "//aio/tools:windows-chromium-path",
]

# All sources, specs, and config files required to serve the app
# and run e2e tests against it
E2E_FILES = APPLICATION_FILES + glob(["tests/e2e/**"])

# Dependencies required to run the e2e tests
E2E_DEPS = APPLICATION_DEPS + [
    "@npm//@angular/build-tooling/bazel/browsers/chromium",
    "@npm//@types/jasmine",
    "@npm//@types/node",
    "@npm//jasmine-spec-reporter",
    "@npm//protractor",
    "@npm//ts-node",
    "//aio/tools:windows-chromium-path",
]

# Poll period for architect rules that watch for changes
ARCHITECT_POLL_MS = 1000

# Stamp npm_link targets for all dependencies that correspond to a
# first-party equivalent pacakge in angular.
link_local_packages(all_aio_deps = collections.uniq(APPLICATION_DEPS + TEST_DEPS + E2E_DEPS))

copy_to_bin(
    name = "application_files_bin",
    srcs = APPLICATION_FILES,
)
